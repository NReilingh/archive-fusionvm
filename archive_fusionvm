#!/bin/zsh

if [ ! -d "$1" ]; then
	echo "Argument 1 must be a directory path to archive."
	exit 3
fi
if [ -z "$2" ]; then
	echo "Argument 2 must be a path to the desired output file."
	exit 3
fi

# $1 is the path to a folder of VMs -- this will be sent directly to hdiutil
# $2 is a destination path for the disk image.

# We need an absolute path for the source.
SRCPATH=$(cd "$1"; pwd)

echo "SRCPATH is $SRCPATH"

running=()

# Test whether running .vmx files are inside our path.
refresh_running() {
	running=(${(@f)"$(vmrun list | grep "$SRCPATH")"})
}

refresh_running

# Save this initial list of .vmx files
vmxlist=($running)

i=0

while true; do
	echo "Current running VM list is $running"

	# If no VMs are running, we can continue.
	[ -z "$running" ] && break

	i=$[ $i + 1 ]
	if [ $i -gt 3 ]; then
		echo "VMs were not suspended after three tries."
		exit 4
	fi

	echo "Attempting to suspend VMs"

	for vmx in $running; do
		echo "Suspending $vmx"
		vmrun suspend "$vmx"
	done

	refresh_running
done

# Test that every VMX in the list has no lockfile; otherwise quit Fusion
echo "Checking for lockfiles for VM list $vmxlist"
for vmx in $vmxlist; do
	echo "Testing lockfile $vmx.lck"
	if [ -f "$vmx.lck" ]; then
		osascript -e 'tell application "VMware Fusion" to quit'
		break
	fi
done

# Create a compressed read-only .dmg. HFS+ seems to be smaller than APFS.
echo "Creating disk image"
hdiutil create -srcfolder "$1" "$2" -fs HFS+

